w > program < w < end

program = ast.node($"program", maybe_array_sep(expr_or_malformed(nl), w))

expr = ast.with_operator_precedence(
  surround(operand, w),
  surround(prefix, w),
  surround(infix, w),
  surround(postfix, w),
)

expr_or_malformed(sync_point) = expr | malformed(sync_point)

malformed(sync_point) = ast.node($"malformed", chars_until(sync_point))

operand = group | number | character | var

prefix =
  ("-" $ Ast.Precedence({"type": "negate"}, 3))

infix =
  (unless(".", ".{") $
         Ast.InfixPrecedence({"type": "method_access"}, 4, 4.5)) |
  (`%` $ Ast.InfixPrecedence({"type": "modulo"},        2, 2.5)) |
  ("*" $ Ast.InfixPrecedence({"type": "multiply"},      2, 2.5)) |
  ("/" $ Ast.InfixPrecedence({"type": "divide"},        2, 2.5)) |
  ("+" $ Ast.InfixPrecedence({"type": "add"},           1, 1.5)) |
  ("-" $ Ast.InfixPrecedence({"type": "subtract"},      1, 1.5))

postfix =
  (function_call -> Node $ Ast.Precedence(Node, 7)) |
  (method_block  -> Node $ Ast.Precedence(Node, 6)) |
  (block         -> Node $ Ast.Precedence(Node, 5))

function_call = ast.node(
  $"function_call",
  "(" > w >
  maybe_array_sep(expr_or_malformed("," | ")" | nl), ",")
  < w < ")"
)

method_block = ast.node(
  $"method_block",
  ".{" > w >
  maybe_array_sep(expr_or_malformed("}" | nl), w)
  < w < "}"
)

block = ast.node(
  $"block",
  "{" > w >
  maybe_array_sep(expr_or_malformed("}" | nl), w)
  < w < "}"
)

group =
  "(" > w >
  expr_or_malformed(")" | nl)
  < w < ")"

number = invalid_number | integer

integer = ast.node($"int", int)

invalid_number = ast.node($"invalid_number", float)

character = ast.node($"char", "'" > char < "'")

var = ast.node($"var", alpha + maybe(word))

w = maybe_many(whitespace | comment)

comment = "#" > chars_until(nl) < ws
